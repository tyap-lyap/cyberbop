plugins {
	id "fabric-loom" version "1.11-SNAPSHOT"
}

version = project.mod_version
group = project.maven_group
base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url 'https://maven.nucleoid.xyz' }
	maven { url "https://maven.terraformersmc.com" } //modmenu

	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	
	maven {
		name = 'GeckoLib'
		url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
		content {
			includeGroup("software.bernie.geckolib")
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api}"
	modImplementation "software.bernie.geckolib:geckolib-fabric-1.21.1:4.8.2"

	modCompileOnly "maven.modrinth:tough-as-nails:dLxSK1xF"
	modCompileOnly "maven.modrinth:glitchcore:lbSHOhee"

//	modImplementation include("eu.pb4:polymer-core:0.9.18+1.21.1")
//	modImplementation include("eu.pb4:polymer-virtual-entity:0.9.18+1.21.1")
	modCompileOnly "eu.pb4:polymer-core:0.9.18+1.21.1"
	modCompileOnly "eu.pb4:polymer-virtual-entity:0.9.18+1.21.1"

	modRuntimeOnly "com.terraformersmc:modmenu:11.0.3"
	modRuntimeOnly "maven.modrinth:emi:1.1.22+1.21.1+fabric"

}

loom {
	accessWidenerPath = file("src/main/resources/cyberbop.accesswidener")
}

processResources {
	def expandProps = [
		"version": rootProject.mod_version,
		"minecraft_version_range": rootProject.minecraft_version_range,
	]

	filesMatching(["fabric.mod.json"]) {
		expand expandProps
	}

	inputs.properties(expandProps)
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	withSourcesJar()
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}
